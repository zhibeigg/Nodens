# damageType 攻击类型
# damageSources 攻击源列表
# defenceSources 防御源列表
# attacker 攻击者
# defender 防御者
# source[id] 源的ID
# source[attribute] 源的属性所属名
# source[value] 源的值
# scale 缩放系数
# 此处可写更高级算式
# 最后返回值为 finalDamage

# 基础伤害(物/法) + 增伤(小怪/boss)
onDamage: |-
  set damage to 0
  set real to 0
  set addon to 0
  set enhancement to 0
  set monsterDamage to 0
  for i in &damageSources then {
    case &i[attribute] [
      when "Physics" -> set damage to math add [ &damage &i[value] ]
      when "Magic" -> set damage to math add [ &damage &i[value] ]
      when "Real" -> set real to math add [ &real &i[value] ]
      when "Monster" -> set addon to math add [ &addon &i[value] ]
      when "Boss" -> set addon to math add [ &addon &i[value] ]
      when "Enhancement" -> set enhancement to math add [ &addon &i[value] ]
      when "Addon" -> set enhancement to math add [ &enhancement &i[value] ]
      when "MonsterAttack" -> set monsterDamage to math add [ &monsterDamage &i[value] ]
    ]
  }
  set defence to 0
  set monsterDefence to 0
  for i in &defenceSources then {
    case &i[attribute] [
      when "Physics" -> {
        if check &damageType == "PHYSICS" then {
          set defence to math add [ &defence &i[value] ]
        }
      }
      when "Magic" -> {
        if check &damageType == "MAGIC" then {
          set defence to math add [ &defence &i[value] ]
        }
      }
      when "MonsterDefence" -> set monsterDefence to math add [ &monsterDefence &i[value] ]
    ]
  }
  set damage to scaled &damage
  set real to scaled &real
  set addon to scaled &addon
  set enhancement to scaled &enhancement
  set defence to scaled &defence
  set monsterDamage to scaled &monsterDamage
  set monsterDefence to scaled &monsterDefence
  case &damageType [
    when "REAL" -> calc "(real+enhancement)*scale"
    when "MONSTER" -> calc "(((monsterDamage+addon)*(1-monsterDefence/(1000+monsterDefence))*1)+enhancement)*scale"
    else calc "(((damage+addon)*(1-defence/(1000+defence))*1)+enhancement)*scale"
  ]
# reason 治疗原因
# regainSources 治疗源列表
# reduceSources 减疗源列表
# healer 治疗者
# passive 受疗者
# source[attribute] 源的属性所属名
# source[value] 源的值
# scale 缩放系数
# 此处可写更高级算式
# 最后返回值为 finalRegain
onRegain: |-
  set a to 0
  for i in &regainSources then {
    set a to math add [ &a &i[value] ]
  }
  for i in &reduceSources then {
    set a to math sub [ &a &i[value] ]
  }
  math mul [ &scale &a ]